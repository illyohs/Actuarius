buildscript {
    repositories {
        maven {
            name 'Shadow'
            url 'http://dl.bintray.com/content/johnrengelman/gradle-plugins'
        }
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:0.8.0'
    }
}

plugins {
  id "com.github.johnrengelman.shadow" version "2.0.4"
}

apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

archivesBaseName = "K9"
group = 'net.minecraftforge.actuarius'
description = "Discord bot for interfacing with the GitHub API"
sourceCompatibility = 1.8
targetCompatibility = 1.8

import org.ajoberstar.grgit.Grgit

if (System.getenv('BUILD_NUMBER') != null) {
    version += ".${System.getenv('BUILD_NUMBER')}"
} else if (new File(projectDir, '.git').exists()) {
    def repo = Grgit.open(project.file('.'))
    version += ".${repo.log().find().abbreviatedId}"
} else {
    version += ".CUSTOM"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
	manifest {
 	   attributes 'Main-Class': 'net.minecraftforge.actuarius.Main',
 	              'Implementation-Version': project.version
	}
}

shadowJar {
	mergeServiceFiles()
}

repositories {
    mavenCentral()
	maven {
		url  "https://jitpack.io"
	}
}

dependencies {
    implementation 'com.sparkjava:spark-core:2.7.2'
    implementation 'org.kohsuke:github-api:1.93'
    implementation 'com.discord4j.discord4j:discord4j-core:v3-SNAPSHOT'
}